10REM > ClockSp
15REM 5.08
20Z%=&0211
30LOMEM=LOMEM+256AND-256
40PRINT"BBC BASIC CPU Timing Program"
50GOTO60
51:
52DEFPROCT:ENDPROC
53:
55RETURN
57:
60DIMB(9),U%4:PROCs:FORC=0TO9:READE:B(C)=4/E:NEXT
70U$="STRINGMANIPULATIONTEST":L=1:I%=0:V%=10:D%=100
80REPEATL=10*L:N%=0:PROCs
90:
100PRINT'"Real REPEAT loop"SPC4;:REPEAT:S=1:E=D%:PROCi
101REPEATUNTILTIME=T%
102FORL%=I%TOE%:C=1:REPEATC=S+C:UNTILC>E:NEXT:W%=TIME:PROCr
103UNTILT>L:PROCp
105:
110PRINT"Variant REPEAT loop ";:REPEAT:S=0.5:E=50:PROCi
111REPEATUNTILTIME=T%
112FORL%=I%TOE%:C=S:REPEATC=S+C:UNTILC>E:NEXT:W%=TIME:PROCr
113UNTILT>L:PROCp
115:
120PRINT"Integer REPEAT loop ";:REPEAT:B%=1:L%=0:PROCi
121REPEATUNTILTIME=T%
122REPEATL%=B%+L%:C%=I%:REPEATC%=B%+C%:UNTILC%>D%:UNTILL%>E%:W%=TIME:PROCr
123UNTILT>L:PROCp
125:
130PRINT"Real FOR loop"SPC7;:REPEATT=0:S=1:E=D%:PROCi
131REPEATUNTILTIME=T%
132FORL%=I%TOE%:FORC=T TOE STEPS:NEXT:NEXT:W%=TIME:PROCr
133UNTILT>L:PROCp
135:
140PRINT"Variant FOR loop"SPC4;:REPEATS=0.5:E=50:PROCi
141REPEATUNTILTIME=T%
142FORL%=I%TOE%:FORC=S TOE STEPS:NEXT:NEXT:W%=TIME:PROCr
143UNTILT>L:PROCp
145:
150PRINT"Integer FOR loop"SPC4;:REPEATS%=1:PROCi
151REPEATUNTILTIME=T%
152FORL%=I%TOE%:FORC%=I%TOD%STEPS%:NEXT:NEXT:W%=TIME:PROCr
153UNTILT>L:PROCp
155:
160PRINT"Trig/Log test"SPC7;:IFCOS0=0PRINTSPC4"<none>":V%=9:B(N%)=0:N%=N%+1:GOTO170
161REPEATC=1.1:E=TANLNC:PROCi
162REPEATUNTILTIME=T%
163FORL%=I%TOE%:E=TANLNC:NEXT:W%=TIME:PROCr
164UNTILT>L:PROCp
165:
170PRINT"String manipulation ";:B%=LENU$DIV2:S%=B%DIV2:Q%=S%+1:C%=2
171REPEATPROCi
172REPEATUNTILTIME=T%
173FORL%=I%TOE%:C%=LENRIGHT$(MID$(U$,B%,Q%)+RIGHT$(U$,S%)+MID$(U$,B%,C%)+LEFT$(U$,S%)+MID$(U$,S%,Q%),LENU$):C%=LENRIGHT$(MID$(U$,B%,Q%)+RIGHT$(U$,S%)+MID$(U$,B%,C%)+LEFT$(U$,S%)+MID$(U$,S%,Q%),LENU$):NEXT:W%=TIME:PROCr
174UNTILT>L:PROCp
175:
180PRINT"Procedure call"SPC6;:REPEATPROCi
181REPEATUNTILTIME=T%
182FORL%=I%TOE%:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT
183PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:NEXT:W%=TIME:PROCr
184UNTILT>L:PROCp
185:
190PRINT"GOSUB call"SPC10;:REPEATPROCi
191REPEATUNTILTIME=T%
192FORL%=I%TOE%:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55
193GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:NEXT:W%=TIME:PROCr
194UNTILT>L:PROCp
195:
200PRINT'SPC11"Average  ";:C=0
210PROCs:FORA%=0TO9:READT:C=C+T*B(A%):NEXT:T=V%
220PROCp
250PRINT'"Compared to a 2.00MHz "Z$
260UNTILL>10^(Z%DIV256AND15)
270*fx151,78,243
280END
290:
300DEFPROCs:Y%=Z%DIV4096:RESTORE600
310IFY%=0AND(INKEY-256<&F4) Y%=2
320IFY%=0THENY%=FNB
330IFY%=4RESTORE800
340IFY%=5RESTORE1000
350READZ$:READI:IF(Z%AND16)=0THENI=1
360IFZ%AND1ENDPROC
370!U%=0:U%?4=0:Y%=U%DIV256:X%=U%AND255
380*FX151,78,62
390*FX16
400ENDPROC
410:
420DEFPROCi:E%=B(N%)*L*1.05:IF(Z%AND1)=0THENA%=4:CALL&FFF1
430T%=TIME+5:ENDPROC
440:
450DEFPROCr:T=W%-T%+0.5:B(N%)=L%/T:ENDPROC
460:
470DEFPROCp:IFN%<10THENC=L%:READE:ELSEE=1
480IFZ%AND&80PRINTC,T,2*T/C" ";
490T=I*C*E/T
500IFT<L T=T+0.5/L:PRINTLEFT$(STR$T,LOGL+2);ELSET=T+5*10^INT(LOGT-LOGL-1):PRINTLEFT$(STR$INTT,LOGL+1);STRING$(LENSTR$INTT-LOGL,"0");
510PRINT"MHz":N%=N%+1:ENDPROC
520:
530DEFFNB
540T%=&49534142:IF!&B809=T%AND?&B80F=52THEN=5
550IF!&B809=T%THEN=?&B815-48
560IF!&8009=T%AND?&800F=52THEN=5
570IF!&8009=T%THEN=?&8015-48
580=0
590:
600DATA"BBC B BASICII",1.0189006
610DATA41.0571084
620DATA41.0318426
630DATA24.2528754
640DATA10.6333739
650DATA10.5533596
660DATA3.72965680
670DATA11.5311908
680DATA4.10374647
690DATA4.89196744
700DATA2.82946382
710:
800DATA"BBC Master BASICIV",1.01551420
810DATA36.0003206
820DATA35.9982909
830DATA21.5435929
840DATA9.19546628
850DATA9.12541140
860DATA3.38775633
870DATA4.63291164
880DATA4.00710039
890DATA4.57998339
900DATA2.69765838
910:
1000DATA"Master BASIC 4r32",1.01579827
1010DATA36.1019811
1020DATA36.1093889
1030DATA21.5354419
1040DATA9.08999746
1050DATA9.02107280
1060DATA3.37835551
1070DATA2.57780376
1080DATA4.00404695
1090DATA4.58095262
1100DATA2.70227976
1110:
