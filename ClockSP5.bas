10REM > ClockSp 
15REM 5.08
20Z%=&0291
30LOMEM=(LOMEM+&100)AND-256
40PRINT"BBC BASIC CPU Timing Program"
50GOTO60
51:
52DEFPROCT:ENDPROC
53:
55RETURN
57:
60DIMB(9),U%4:PROCs:FORC=0TO9:READE:B(C)=4/E:NEXT
70U$="STRINGMANIPULATIONTEST":L=1:I%=0:V%=10:D%=100
80REPEATL=10*L:N%=0:PROCs
90:
100PRINT'"Real REPEAT loop    ";:REPEAT:S=1:E=D%:PROCi
101T%=TIME
102FORL%=I%TOE%:C=1:REPEATC=S+C:UNTILC>E:NEXT:T%=TIME-T%:PROCr
103UNTILT>L:PROCp
105:
110PRINT"Variant REPEAT loop ";:REPEAT:S=0.5:E=50:PROCi
111T%=TIME
112FORL%=I%TOE%:C=S:REPEATC=S+C:UNTILC>E:NEXT:T%=TIME-T%:PROCr
113UNTILT>L:PROCp
115:
120PRINT"Integer REPEAT loop ";:REPEAT:B%=1:L%=0:PROCi
121T%=TIME
122REPEATL%=B%+L%:C%=I%:REPEATC%=B%+C%:UNTILC%>D%:UNTILL%>E%:T%=TIME-T%:PROCr
123UNTILT>L:PROCp
125:
130PRINT"Real FOR loop       ";:REPEATT=0:S=1:E=D%:PROCi
131T%=TIME
132FORL%=I%TOE%:FORC=T TOE STEPS:NEXT:NEXT:T%=TIME-T%:PROCr
133UNTILT>L:PROCp
135:
140PRINT"Variant FOR loop    ";:REPEATS=0.5:E=50:PROCi
141T%=TIME
142FORL%=I%TOE%:FORC=S TOE STEPS:NEXT:NEXT:T%=TIME-T%:PROCr
143UNTILT>L:PROCp
145:
150PRINT"Integer FOR loop    ";:REPEATS%=1:PROCi
151T%=TIME
152FORL%=I%TOE%:FORC%=I%TOD%STEPS%:NEXT:NEXT:T%=TIME-T%:PROCr
153UNTILT>L:PROCp
155:
160PRINT"Trig/Log test"SPC(7);:IFCOS0=0PRINTSPC4"<none>":V%=9:B(N%)=0:N%=N%+1:GOTO170
161REPEATC=1.1:E=TAN(LN(C)):PROCi
162T%=TIME
163FORL%=I%TOE%:E=TAN(LN(C)):NEXT:T%=TIME-T%:PROCr
164UNTILT>L:PROCp
165:
170PRINT"String manipulation ";:B%=LENU$DIV2:S%=B%DIV2:Q%=S%+1:C%=2
171REPEATPROCi
172T%=TIME
173FORL%=I%TOE%:C%=LEN(RIGHT$(MID$(U$,B%,Q%)+RIGHT$(U$,S%)+MID$(U$,B%,C%)+LEFT$(U$,S%)+MID$(U$,S%,Q%),LENU$)):C%=LEN(RIGHT$(MID$(U$,B%,Q%)+RIGHT$(U$,S%)+MID$(U$,B%,C%)+LEFT$(U$,S%)+MID$(U$,S%,Q%),LENU$)):NEXT:T%=TIME-T%:PROCr
174UNTILT>L:PROCp
175:
180PRINT"Procedure call"SPC(6);:REPEATPROCi
181T%=TIME
182FORL%=I%TOE%:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT
183PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:PROCT:NEXT:T%=TIME-T%:PROCr
184UNTILT>L:PROCp
185:
190PRINT"GOSUB call"SPC(10);:REPEATPROCi
191T%=TIME
192FORL%=I%TOE%:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55
193GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:GOSUB55:NEXT:T%=TIME-T%:PROCr
194UNTILT>L:PROCp
195:
200PRINT'SPC(11)"Average  ";:C=0
210PROCs:FORA%=0TO9:READT:C=C+T*B(A%):NEXT:T=V%
220PROCp
250PRINT'"Compared to a 2.00MHz "Z$
260UNTILL>10^((Z%DIV256)AND15)
270*fx151,78,243
280END
290:
300DEFPROCs:Y%=Z%AND&F000:RESTORE600
310IFY%=0AND(INKEY(-256)>1ORINKEY(-256)<&F0) Z%=Z%+&2000
320IFY%=0THENY%=FNB
330IFY%=4RESTORE800
340IFY%=5RESTORE1000
350READZ$:READM:IF(Z%AND16)=0THENM=1
360IFZ%AND1ENDPROC
370!U%=0:U%?4=0:Y%=U%DIV256:X%=U%AND255
380*FX151,78,62
390*FX16,0,0
400ENDPROC
410:
420DEFPROCi:E%=B(N%)*L*1.05:IF(Z%AND&1)=0THENA%=4:CALL&FFF1
430ENDPROC
440:
450DEFPROCr:T=T%+0.5:B(N%)=L%/T:ENDPROC
460:
470DEFPROCp:IFN%<10THENC=L%:READE:ELSEE=1
480IFZ%AND&80PRINTC,T,2*T/C" ";
490T=(M*C*E/T)+0.5/L:REM T=T*10000
500IFT<L PRINTLEFT$(STR$(T),LOG(L)+2);ELSEPRINTLEFT$(STR$(INT(T)),LOG(L)+1);STRING$(LEN(STR$(INT(T)))-(LOG(L)+1),"0");
510PRINT"MHz":N%=N%+1:ENDPROC
520:
530DEFFNB
540T%=&49534142:IF!&B809=T%AND?&B80F=&34THEN=5
550IF!&B809=T%THEN=?&B815-48
560IF!&8009=T%AND?&800F=&34 THEN=5
570IF!&8009=T%THEN=?&8015-48
580=0
590:
600DATA"BBC B BASICII",1.01889653
610DATA41.0571084
620DATA41.1571515
630DATA24.2528754
640DATA10.6368630
650DATA10.5577759
660DATA3.73191332
670DATA11.5782319
680DATA3.94371473
690DATA4.89196739
700DATA2.68503938
710:
800DATA"BBC Master BASICIV",1.01551517
810DATA36.0003069
820DATA36.1177558
830DATA21.5435929
840DATA9.19813085
850DATA9.12951308
860DATA3.39001966
870DATA4.67967639
880DATA3.85715860
890DATA4.57998751
900DATA2.55346259
910:
1000DATA"Master BASIC 4r32",1.0158
1010DATA36.1019811
1020DATA36.1721433
1030DATA21.5354440
1040DATA9.09267815
1050DATA9.02518887
1060DATA3.38061456
1070DATA2.62327582
1080DATA3.85322675
1090DATA4.58095244
1100DATA2.55764113
1110:
